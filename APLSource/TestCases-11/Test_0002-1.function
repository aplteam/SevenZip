 r←Test_0002(stopFlag batchFlag);zipFilename;list;filename;myZipper;rc;more;found;⎕TRAP
 ⍝ Compress two files into a non-existing file.
 r←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 zipFilename←∆OutputPath,'zip_002.zip'
 list←∆InputPath∘,¨'ZIP.DWS' 'ZIP.dyalog'

⍝ Delete any left overs
 #.FilesAndDirs.DeleteFile zipFilename

⍝ The test
 myZipper←⎕NEW ##.SevenZip(,⊂zipFilename)
 rc←myZipper.Add list
 →GoToTidyUp 0∨.≠3⊃rc

 (rc more)←myZipper.List 0
 →GoToTidyUp~#.FilesAndDirs.Exists zipFilename
 →GoToTidyUp rc≠0
 →GoToTidyUp 2≠⍴more

 (rc more)←myZipper.Unzip ∆TempPath
 →FailsIf rc≠0
 found←'recursive'#.FilesAndDirs.ListDirs ∆TempPath
 →GoToTidyUp(+/∆InputPath∊'\/')≠⍴found
 r←∆OK

∆TidyUp:
 {}#.FilesAndDirs.RmDir ∆TempPath
 #.FilesAndDirs.DeleteFile zipFilename
