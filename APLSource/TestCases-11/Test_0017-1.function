 r←Test_0017(stopFlag batchFlag);zipFilename;filename;myZipper;rc;more;⎕TRAP;found;tempPath
 ⍝ Add two files `∆⍙1` and `∆⍙2` living in a folder `∆⍙` to an archive.
 r←∆Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 tempPath←∆OutputPath,'..\Testcase_0017\∆⍙'
 'Create!'#.FilesAndDirs.CheckPath tempPath
 #.APLTreeUtils.WriteUtf8File(tempPath,'\∆⍙1')'First test file'
 #.APLTreeUtils.WriteUtf8File(tempPath,'\∆⍙2')'Second test file'

 zipFilename←∆OutputPath,'zip_001.zip'
⍝ Delete any left overs
 #.FilesAndDirs.DeleteFile zipFilename

⍝ The test
 myZipper←⎕NEW ##.SevenZip(,⊂zipFilename)
 rc←myZipper.Add tempPath
 →GoToTidyUp 0≠⊃rc
 {}#.FilesAndDirs.RmDir tempPath
 ⎕DL 0.1
 →GoToTidyUp #.FilesAndDirs.IsDir tempPath
 rc←myZipper.Unzip tempPath
 ⍝ Note that `List` would return `__` for `∆⍙` at this point. This is a 7zip problem!
 →GoToTidyUp 0≠⊃rc
 →GoToTidyUp'∆⍙1' '∆⍙2'≢{⊃,/1↓⎕NPARTS ⍵}¨⊃#.FilesAndDirs.Dir tempPath,'\∆⍙\*'

 r←∆OK

∆TidyUp:
 {}#.FilesAndDirs.RmDir¨∆TempPath tempPath
 #.FilesAndDirs.DeleteFile zipFilename
